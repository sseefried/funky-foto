{-# LANGUAGE CPP, TemplateHaskell, OverloadedStrings #-}
{-# LANGUAGE ScopedTypeVariables, TypeOperators #-}

module Handler.Images
  (
    -- Route Handlers
    getOriginalImageR
  , getBugImageR
  , getSpinnerImageR
  , getPreviewImageR
  , getInputImageR
  , getResultImageR

    -- Helpers
  , compileEffect
  , saveInputImage
  , imageHash

  ) where

-- standard libraries
import Control.Monad
import Control.Concurrent.MVar
import qualified Data.ByteString.Lazy       as BL
import qualified Data.ByteString.Lazy.Char8 as C
import Data.List
import System.IO
import System.Directory
import System.FilePath
import System.Cmd
import System.Posix.Types
import System.Posix.IO
import System.Posix.Process
import System.Exit

import Yesod.Helpers.Static


-- friends
import Foundation
import Settings
import Model


-- | The dimensions to use for a scaled version of an image. If scaling is not
--   to be performed (i.e. the original image), Nothing is returned.
--
scaledDimensions :: ImageSize -> Maybe (Int, Int)
scaledDimensions Normal = Nothing
scaledDimensions Thumb  = Just (96, 96)
scaledDimensions Mobile = Just (44, 44)


-- | Return the un-altered preview image.
--
getOriginalImageR :: Handler ()
getOriginalImageR = sendJpeg Settings.previewImage


-- | Return the "bug" image for generated images whose effect code doesn't compile.
--
getBugImageR :: ImageSize -> Handler ()
getBugImageR size = getScaledImage size Settings.bugImage


-- | Return the "spinner" image for displaying whilst images are being generated.
--
getSpinnerImageR :: Handler ()
getSpinnerImageR = sendFile "image/gif" Settings.spinnerImage


-- | Retrieve preview image resources.
--
getPreviewImageR :: ImageSize -> String -> Handler ()
getPreviewImageR size name = do
  foundation <- getYesod
  mbResult   <- runDB $ do { getBy $ UniqueEffect name }

  case mbResult of
    Nothing         -> getBugImageR size
    Just (_,effect) -> do
      let effectHash    = codeHash effect
          inputImgFile  = Settings.previewImage
          resultImgFile = imageFile (cacheDir foundation) ("preview-" ++ effectHash)

      res <- generateImage effect inputImgFile resultImgFile
      case res of
        True  -> getScaledImage size resultImgFile
        False -> getBugImageR size


-- | Retrieve an image that has been uploaded.
--
getInputImageR :: String -> Handler ()
getInputImageR imgHash = do
  foundation <- getYesod
  sendJpeg $ imageFile (cacheDir foundation) imgHash


-- | Save an input image to disk.
--
saveInputImage :: FileInfo -> Handler FilePath
saveInputImage fi = do
  foundation <- getYesod
  let contents = fileContent fi
      imgHash  = imageHash fi
      filepath = imageFile (cacheDir foundation) imgHash
  liftIO $ BL.writeFile filepath contents
  return filepath


-- | Retrieve image resource that is the result of running an effect.
--
getResultImageR :: String -> String -> Handler ()
getResultImageR name inImgHash = do
  foundation <- getYesod
  mbResult   <- runDB $ do { getBy $ UniqueEffect name }
  case mbResult of
    Nothing         -> getBugImageR Normal
    Just (_,effect) -> do
      let effectHash    = codeHash effect
          inputImgFile  = imageFile (cacheDir foundation) inImgHash
          resultImgFile = imageFile (cacheDir foundation) (effectHash ++ "-" ++ inImgHash)
      res <- generateImage effect inputImgFile resultImgFile
      case res of
        True  -> sendJpeg resultImgFile
        False -> getBugImageR Normal


-- | Retrive an image resource that is generated by running an effect. See
--   getResultImageR and getPreviewImageR.
--
generateImage :: Effect -> FilePath -> FilePath -> Handler Bool
generateImage effect inImgFile outImgFile = do
  exists <- liftIO $ doesFileExist outImgFile
  case exists of
    True  -> return True
    False -> do
      compileRes <- compileEffect effect
      case compileRes of
        (Left _)       -> return False
        (Right binary) -> do
          runEffect binary inImgFile outImgFile
          return True


-- | Retrieve a scaled version of an image. If it does not exist, generate it. Assumes
--   that the original already exists. See getBugImageR and getPreviewImageR.
--
getScaledImage :: ImageSize -> FilePath -> Handler ()
getScaledImage size originalImgFile = do
  imgFile <- maybe (return originalImgFile) (\dim -> scaleImage dim originalImgFile) (scaledDimensions size)
  sendJpeg imgFile

  where
    scaleImage :: (Int, Int) -> FilePath -> Handler FilePath
    scaleImage (w, h) inImgFile = do
      let outImgFile = (dropExtension inImgFile) ++ "-" ++  dimensions <.> "jpg"
          dimensions = (show w) ++ "x" ++ (show h)

      liftIO $ do
        exists <- doesFileExist outImgFile
        unless exists $ do
          rawSystem "convert" ["-scale", dimensions, inImgFile, outImgFile] >> return ()

      return outImgFile


-- | Helper function for sending a JPEG file. Sets the content type appropriately.
--
sendJpeg :: FilePath -> Handler ()
sendJpeg = sendFile "image/jpeg"


-- | For a given image "basename", return its location on disk.
--
imageFile :: FilePath -> String -> FilePath
imageFile cacheD imgName = cacheD </> "images" </> imgName <.> "jpg"


-- | Produce a hash string for an image file based on its contents.
--
imageHash :: FileInfo -> String
imageHash = base64md5 . fileContent


-- | Produce a hash of an effect's code string.
--
codeHash :: Effect -> String
codeHash effect = base64md5 $ C.pack (effectCode effect)


-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
-- Helpers for compiling and running effects
--

-- | Compile the effect code. Return either the path to the compiled binary (Right) or
--   the compiler error (Left).
--
compileEffect :: Effect -> Handler (Either String FilePath)
compileEffect effect = do
  foundation <- getYesod
  let hash          = codeHash effect
      codeDir       = (cacheDir foundation) </> "code"
      effectSrcFile = codeDir </> hash <.> "hs"
      effectExeFile = codeDir </> hash

  exists <- liftIO $ doesFileExist effectExeFile
  case exists of
    True  -> return (Right effectExeFile)
    False -> do
#ifdef HAS_CUDA
      let backend = "CUDA"
#else
      let backend = "Interpreter"
#endif
      liftIO $ writeFile effectSrcFile $ (effectCodeWrapper foundation) ++ (indent $ effectCode effect)
      res <- liftIO $ runProcess "ghc" True ["--make", "-DBACKEND=" ++ backend, effectSrcFile, "-o", effectExeFile] Nothing

      case res of
        (Just output) -> return (Left output)
        Nothing       -> return (Right effectExeFile)

  where
    indent = concat . intersperse "\n" . map ("    " ++) . lines


-- | Obtain the CUDA lock then run the effect. Use bitmap files as the intermediate
--   image file format. Remove the bitmap files on completion.
--
runEffect :: FilePath -> FilePath -> FilePath -> Handler ()
runEffect effectBin imageInJpg imageOutJpg = do
  foundation <- getYesod
  scratchDir <- liftIO $ getTemporaryDirectory

  let imageInBmp   = scratchDir </> "in"  <.> "bmp"
      imageOutBmp  = scratchDir </> "out" <.> "bmp"

  liftIO $ withMVar (cudaLock foundation) $ \() -> do
    liftIO $ jpgToBmp imageInJpg imageInBmp
    _ <- runProcess effectBin False [imageInBmp, imageOutBmp] Nothing
    liftIO $ bmpToJpg imageOutBmp imageOutJpg
  _ <- liftIO $ mapM removeFile [imageInBmp, imageOutBmp]

  return ()


-- | Convert a JPEG file to a bitmap file.
--
jpgToBmp :: FilePath -> FilePath -> IO ()
jpgToBmp jpgFile bmpFile = rawSystem "convert" [jpgFile, bmpFile] >> return ()


-- | Convert a bitmap file to a JPEG file.
--
bmpToJpg :: FilePath -> FilePath -> IO ()
bmpToJpg bmpFile jpgFile = rawSystem "convert" [bmpFile, jpgFile] >> return ()


-- | Run a separate process, redirecting stdout to the returned string.
--
runProcess :: FilePath
           -> Bool
           -> [String]
           -> Maybe [(String, String)]
           -> IO (Maybe String)
runProcess cmd path args env = do
  (outr, outw) <- createPipe
  cpid   <- forkProcess $ doProcess outw

  closeFd outw
  hr     <- fdToHandle outr
  outstr <- hGetContents hr
  status <- getProcessStatus True False cpid

  case status of
    (Just (Exited ExitSuccess)) -> return Nothing
    _                           -> return (Just outstr)

  where
    doProcess :: Fd -> IO ()
    doProcess outw = do
      _ <- dupTo outw stdOutput
      _ <- dupTo outw stdError
      executeFile cmd path args env

